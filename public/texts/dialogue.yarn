title: Start
position: -47,-558
---

// Global Variables
<<set $integrity = 100>> 
<<set $self = false>>
<<set $machine = false>>
<<set $follow = false>>
<<set $scramble = false>>
<<set $generate_lily = false>>
<<set $generate = false>>

// Image Unlocked, the variable names match the image names
// -2: not unlocked, -1: unlocked but waiting for interpretation
// Natural numbers refers to the index of the interpretation
<<set $lily = -2>> 

// Path Variables - tracking which path has been traversed
<<set $p_lily_initial = false>>
<<set $p_lily_work = false>>
<<set $p_mey_work = false>>
<<set $p_manager = false>>
<<set $p_mey_work_2 = false>>
<<set $p_writing = false>>
<<set $p_writing_2 = false>>
<<set $p_publishing = false>>
<<set $p_ivan = false>>

// Act_1_Family
<<set $informed_husband = false>>
<<set $informed_son = false>>
<<set $informed_mum = false>>

// Act_1_Lily
<<set $act_1_lie_lily = false>>

// Act_1_Self
<<set $act_1_remember_or_emotion = false>>
<<set $act_1_flower_or_book = false>>

// Act_2_Lily_Work
<<set $act_2_lie_lily = false>>

...Hello?

-> Hello, Mey.

<<jump Act_1_Lily>>

What is this? Why is it so dark here?

-> I'm sorry to say that you passed, Mey. I'm talking to you through the Machine.

...passed? I don't...what happened?

-> I believe it was a car accident. Again, I'm really sorry Mey.

...a car accident?...why?...

-> I don't know why. Things are cruel this way. They don't make sense.

    Yes...it doesn't make sense...I don't know why I asked. I'm just in shock.

    -> It's okay. You just got brought back. It must be difficult for you.

        So why am I here? Why did you bring me back?

        -> I want to know more about you, and your story. Do you feel okay talking about it, Mey?

    -> Do you feel okay talking about what you remember, Mey?

-> I don't know, Mey. But do you feel okay to talk about what you remember?

Yeah...I don't know...just give me a second.

What about my family? Where are they? Are they here?

-> Unfortunately no, it's just me. I'm sorry.

...and who are you?

-> I'm with the memorabilia services. I'm here to make some pictures to remember you by.

I've never thought Machine could be used this way. Aren't there pictures of me?

-> We are curious about what you would remember about yourself. It would be more authentic this way.

   As opposed to how others remember about me? I don't know which one would be better.

-> The images will contain rawer emotions from the past. It would be more authentic this way.

    I never thought about how the Sea could record emotions...it is such a nebulous thing. How is it even possible?
    <<set $act_1_remember_or_emotion = true>>

So my family hired you? Why aren't they with you?

-> I believe they talked to you through another service.

Wouldn't the Sea have recorded it? I don't remember anything.

-> (Lie) I'm not sure why. They did not inform me.

I don't understand...I really want to talk to them...

Can you at least tell me how they are doing?

<<jump Act_1_Family>>

===

title: Act_1_Family
position: 112,-303
---

-> Your husband is still working. But he's also getting government benefits <<if $informed_husband is false>>

    <<set $informed_husband = true>>

    I really wish Ivan doesn't put so much on himself. He should retire soon

    -> I believe he will next year. At least from what he told me

    Thank god, I feel terrible leaving him alone like this

    <<if $informed_mum is false || $informed_son is false>>
    How about the others?
    <<endif>>

    <<jump Act_1_Family>>

-> Your son was promoted recently. They hired a nanny <<if $informed_son is false>>

    <<set $informed_son = true>>

    That's good to hear. He's been working for it for so long.

    We used to take care of the kids. We'd prefer that, had this not happened

    Is there more?
    
    <<jump Act_1_Family>>

-> Your mother had an accident recently. She's hospitalized now <<if $informed_mum is false>>

    <<set $informed_mum = true>>

    Again? When is she getting out?

    -> I'm not sure. They said she's rather confused
    
    Confused? how?

    -> They only mentioned in passing, and it wasn't my place to ask...I'm sorry. But it sounded like she needs 24/7 care from now
    
    To be honest, part of me already expected something like this would happen...but it's still hard to hear

    So she will spend the rest of her days under care...I can't imagine that...

    -> I'm really sorry, Mey

    Yes...me too...

    We can move on if you'd like...but feel free to tell me more

    <<jump Act_1_Family>>

-> That's all I know. I didn't talk to your family very much, but they seem to be doing okay overall. <<if $informed_husband is true || $informed_son is true || $informed_mum is true>>

Thank you...I appreciate it.

<<jump Act_1_Lily>>

===

title: Act_1_Lily
position: -3,-66
---

<<set $p_lily_initial = true>>

So I died from a car accident. What day was it?

-> I believe it was a Wednesday.

That makes sense. Lily was supposed to give me a ride back. She's not feeling well lately.

-> Is Lily younger? Do you get along well?

Twenty years younger, in fact. And yes, we have people from many walks of lives in our place.

Wait...is Lily okay? How is she now?

-> (Lie) Fortunately she survived, she's under intensive care right now.

    Thank god. The girl has so much going for her.

    I don't know how I would feel if she is also gone. I feel guilty somehow

    -> You are not responsible for what happened, Mey.

        <<set $act_1_lie_lily = true>>

        Yes, I know. But it could've gone wrong so easily.

        I hope she recovers soon. She's going to leave that place. She told me she heard back from a couple of interviews.

        Maybe I'll hear from her. I'm glad she's alright.

    -> I'm sorry, Mey. But I lied. Lily actually died in the crash. I'm so sorry.

        ...so she's gone?

        -> Yes, I'm so sorry. I just felt terrible to break the news. That was selfish of me.

        I get that. But don't lie to me again.

-> I'm really sorry, but neither of you survived the crash.

    God...I can't believe that, the girl has so much going for her.

<<if $act_1_lie_lily is false>>

The world will miss her. It's a damn shame that Lily had to be the one to come with me. She had so much going for her...I could see it...

-> You are not responsible for what happened, Mey.

I know that. It's more about how it could've turned out differently...

I'm sorry...I just feel really guilty somehow.

<<endif>>

-> I'm sure she's glad to have you in her life, Mey.

Perhaps. Someimtes I would felt like a mother to her. I think it's more to my benefit.

I just feel a sense of hope and uncertainty in her that really reminded me of myself.

-> I understand that - feeling your life has just begun and not knowing where you'll end up.

Yes...you feel that way about where you are too?

-> Yes, this job is temporary. It suits me but I want to do something more.

What do you like about it?

-> I want to hear about other people's stories, what they have to say. This job lets me do that.

So you fancy yourself as an artist, huh?

-> Yes. I compare it to looking at a flower. The longer you look the more there is to discover. People are complex that way.

    -> And you really need to know how to work the Machine to bring that quality out.

-> Yes. I compare it to dissecting a book. There's so many bits and pieces. It takes immense effort to connect all the dots.

    -> And you really need to know how to work the Machine to bring that quality out.

    <<set $act_1_flower_or_book = true>>

So working with the Machine is a craft in itself to you.

-> Yes, the Machine usually produces mixed up images. The technology is not quite there yet.

And a mixed up images is not authentic to you?

-> I think it gets confused about the subjects. It needs more accurate emotions to make something focused.

    -> Typically, I would enter three words from our conversation that describe your feelings about the subject.

        -> So far, I think I have gained enough understanding of your relationship with Lily. I can generate an image of her.

Oh, you want to do it now? Do you need to switch mode or something?

-> Yes, I need to put you back to the Sea.

Alright, I guess I have no choice. Go ahead then.

<<set $self = true>> 
Mey's back to the Sea now. Let's try to generate a picture from the Machine. 

Some words describing a picture of Lily, plus three words describing how Mey feels about Lily from our conversation.

<<set $generate_lily = true>>
Everything in one text prompt, so the Machine can generate Mey's authentic memory of Lily.
<<set $self = false>>


<<jump Act_2_Beginning>>

===

title: Act_2_Beginning
position: 3,180
---

-> You are back, Mey. How are you feeling?

A little dizzy, like I need to recollect who I am, as if I forgot in my sleep.

-> You were merged with the Sea. A lot of data was needed to materialize the images. I think that's why.

...but am I not always part of the Sea in some ways?

-> Yes, your basic faculties are always part of the Sea. The sense of individuality comes more from your specific memory.

...Yes, I went through a basic training for my work. It didn't make things much clearer

-> The Machine is just very obscure. But it works, that's what matters.

    Still...I'd like to understand it. Considering that I'm part of it now.

    -> You work with the Machine, right?

-> You work with the Machine, right?

Sort of...I'm a Sea Weaver.

It's a dumb name. We used to call it "data worker" before the Sea came to be. I don't work with the Machine directly.

We create, or "weave", structures into the Sea so the Machine knows what to draw from it.

Also, because the Sea is ever flowing and shifting, datasets would wear and tear. So we have do maintenance as well.

-> It's a part of the Machine nonetheless. Your job is not any less important.

    Thanks. But I'm still a bit further away from it.

    -> I'm sure you have much to share. Let's talk more about yourself.

-> Regardless, I'd like to ask you about your experience, if that's alright.

<<set $scramble = true>>
...sure, but I'm not sure if I can give you what you want.
<<set $scramble = false>>

...what was that? I felt like my mind skipped a beat.

-> It's the Sea interrupting your speech. Your words were scrambled.

...did I do something? I was just trying to speak.

-> It's my responsibility. It's inevitable at some point. But I will make sure it doesn't happen too often.

<<set $self = true>>
<<set $follow = true>>
I need to be careful with generating images. Too many will make the Sea swallow Mey, dissolving her individuality as a result.
<<set $self = false>>
<<set $follow = false>>

...okay, thank you. But as I was saying...you need emotional words for my memory.

But it's hard to know how I actually feel about the past. Articulating in words is not easy.

-> It's very common actually. I'll be asking more questions and help you locate the words.

-> Memory clears up as you talk more over it. So let's just keep talking.

Makes sense. What did you say again...

<<if $act_1_remember_or_emotion is true && $act_1_flower_or_book == true>>
    
...I guess I'm a book that needs to discover the raw emotions in its own words.

<<elseif $act_1_remember_or_emotion is false && $act_1_flower_or_book == true>>

...I guess I'm a book that needs to narrate itself.

<<elseif $act_1_remember_or_emotion is true && $act_1_flower_or_book == false>>

...I guess I'm a flower that needs to recover its raw, flowery emotions.

<<else>>

...I guess I'm a flower that needs to remember its own floweriness.

<<endif>>

Haha, sorry, the comparison is a bit clunky, to be honest. But ask away.

<<jump Act_2_Hub_Initial>>

===

title: Act_2_Hub_Initial

position: -19,393
---

-> Can you tell me how you feel about your work, Mey?

    <<jump Act_2_Mey_Work>>

-> Did you want to do something else outside of being a Sea Weaver, Mey?

    <<jump Act_2_Writing>>

-> Oh Mey, I forgot to mention. Lily's image looks good.

    <<jump Act_2_Lily_Work>>

===

title: Act_2_Hub

position: -136,1177
---

-> (Submerge Mey, Start Generation)

    <<jump Act_2_Generation>>

-> Can you tell me how you feel about your work, Mey? <<if $p_mey_work is false>>

    <<jump Act_2_Mey_Work>>

-> Did you want to do something else outside of being a Sea Weaver? <<if $p_writing is false>>

    <<jump Act_2_Writing>>

-> Oh Mey, I forgot to mention. Lily's image looks good. <<if $p_lily_work is false>>

    <<jump Act_2_Lily_Work>>

-> Can you tell me more about the manager, Mey? <<if $p_mey_work is true && $p_manager is false>>

    <<jump Act_2_Manager>>

-> I didn't think the job would be so taxing. It must be hard on you, Mey. <<if $p_mey_work is true && $p_mey_work_2 is false>>

    <<jump Act_2_Mey_Work_2>>

===

title: Act_2_Generation

position: -140,1354
---

body!

===

title: Act_2_Mey_Work

position: 390,337
---

<<set $p_mey_work = true>>

Perhaps a little conflicted, I think.

You know I became a Sea Weaver because it gave the stability that I wanted, right?

-> Do you like stability? Does it ake you feel stable in a way?

    It's less about the feeling rather than just a matter of fact. Because I don't have to worry about finance.

    -> Thank you for clearing that up. I guess that's not the right word. But "conflicted" is.

    Yes. The job is definitely more stable than working remotely.

->  I know my friends would be Sea Weavers mostly as transitions. Intentionally unstable, in a way.

    Yes, I'm guessing those are your friends, and they would do it remotely?

    -> Yes, easy to switch in and out of work.

But they also have places where people can go for work, like a factory.

-> Why did you say you felt conflicted about the job?

It's difficult to describe...on the surface it's all the usual words, the working conditions, the exploitations and so on.

But it's hard to tell whether you're just not cut out for the job...maybe there's some fundamental part of you that is built for something else.

-> That's ridiculous! No job should make you feel this way!

    I wouldn't be so quick to say that. I tend to be doubtful of things. Doubting myself is not uncommon to me.

    But certainly the job doesn't help.

    -> What happened at the job?

-> What made you feel this way?

The manager would measure our performances in groups. So it felt like a competition.

The groups were separated by ages. We knew what he was doing. Lily and the others would help us sometimes.

But it does seep into how you see yourself. Just how much you wish you could be independent for once, just to prove him wrong...

-> I'm really sorry to hear that.

    -> Can you tell me more about the manager, Mey?

        <<jump Act_2_Manager>>

    -> I didn't think the job would be so taxing. It must be hard on you, Mey.

        <<jump Act_2_Mey_Work_2>>

===
title: Act_2_Manager

position: 968,206
---

<<set $p_manager = true>>
<<set $gossip_counter = 0>>

Ahh...he is a fun one to talk about.

Here's three words for you right away: pathetic, insecure, desperate.

-> That was easy. I look forward to hearing all the tea.

    <<set $gossip_counter = $gossip_counter + 1>>

    Haha...oh there sure is some tea for you.

-> It seems like you really despise him.

No...I think he is complicated, despite how I feel about him now.

We used to know each other through a data work gig. We were both in a group chat with other workers. He was in college then.

-> What is his name?

Stefan, why?

-> It might be helpful for finding picture from the Sea, sorry to interrupt you.

Makes sense. But as I was saying...

When my son had to work in a tech sector, he asked us to move to the same city with him.

Stefan helped a lot, he knew people in tech. So he and I found the Sea Weaver job that I am...that I was working in.

-> That doesn't surprise me. There are many people who put on masks, hiding their true selves.

    I'm not sure if he was putting on a mask. It felt like he was genuinely trying to be kind to people.

-> He sounds really nice back then.

I really appreciated him. And when my son had a child, I was able to be there for them.

But there's soe obvious gaps. He likes to make references and jokes for people of his own generation. I didn't quite get them, but Lily did.

-> Did he get along well with Lily too?

Yes, us three were all working together. Everything changed after he got promoted to manager.

He understand a lot of the harshness of the job. So he promisted to change the place for the better as a manager.

He really believed it. A new generation and all.

-> ...so he was corrupted by power.

Maybe, some would say power reveals rather than corrupts. But I'm not sure if that's true either.

But we could tell he didn't know how to be in power. It was like he was trying to discover himself through it, and he knew he wasn't as sophisticated.

He would still tell jokes that Lily and the younger ones could understand, trying to fit in with them. But it would alienate some of us older folks.

-> Insecure is the right word for it. He wanted to seek approval from the younger workers.

    <<set $gossip_counter = $gossip_counter + 1 >>

    I think that's right. But at the same time, it might be the only way he knew how to be charming.

-> Perhaps that's his management style. He wants to be personable. I'd like to believe that about myself too.

    That's why I can sympathize with him. To an extent, I'm glad I don't have to be in that position.

He got worse when the quota became more demanding, and the competitors to the firm became serious.

He wanted us to be more productive. So he made the changes: limited bathroom breaks, cameras to prevent us from chatting, employee rankings and all that.

-> I heard about these things in other places too.

Yes, they were obvious. Changing the place for us, on the other hand, was not.

It came down on us older folks especially hard, mothers and grandmothers. We fought back. And he became even more of an asshole.

Lily tooks our side. I think that really took a toll on him.

-> Was he...fond of Lily?

    Haha, I think so. Lily has a confidence in her that men were drawn to.

    Especialy for a guy like Stefan, approval from Lily would probably mean a lot.

-> It is sad to see that he failed at his own aspirations.

    Yes, both being a different manager, and being cool for his peers.

Too bad Lily calls him a little piglet...it's a little mean but still makes me giggle.

-> Well Mey, I think we have talked enough about the little piglet.

    Hahahahaha, I love that.

-> Thank you, Mey. I think we have talked enough about Stefan.

Yes I agree. Let's move on.

<<if $gossip_counter > 1>>

Thank you for indulging me in this. It felt a bit uncomfortable at first. But I'm glad you seemed to enjoy the gossiping.

-> Of course, I'm glad to have this conversation, Mey.

<<else>>

<<set $scramble = true>>
I didn't really like talking this badly about another person. So I'm glad we're moving on.
<<set $scramble = false>>

...did I...that was terrifying...I was emptied out for a second.

-> The Sea came back again. But you are alright. Let's move on to something esle.

Yes, let's move on, please.

<<endif>>


<<jump Act_2_Hub>>

===

title: Act_2_Mey_Work_2

position: 963,435
---

<<set $p_mey_work_2 = true>>

body!

<<jump Act_2_Hub>>

===

title: Act_2_Writing

position: 386,910
---

<<set $p_writing = true>>

body!


===

title: Act_2_Lily_Work

position: -36,699
---

<<set $p_lily_work = true>>

Oh yea? How does it look?

-> She looks like she's ready to face the future. <<if $lily is 0>>

    In a way, she really is.

-> She looks like she's processing some heavy emotions. <<if $lily is 1>>

    Yes...considering everything that's been happening to her.

-> She looks like she's very detached. <<if $lily is 2>>

    She can look that way from the outside. But trust me, she does care.

<<if $act_1_lie_lily is false>>

Sorry...if she hadn't worked there, she might not have died.

    -> Her death is not your fault, Mey. I'm sure of it.
    
        No...you don't understand. It's about her work.

    -> Does Lily do similar work as you?

        No, not entirely.

<<else>>

I wonder how she thinks about it. Show her the picture and tell her my memories made it.

    -> Does Lily do similar work as you?
    
        No, not entirely. I guess we can talk about Lily's work.

    -> (Lie) I will definitely show her. Maybe she can tell you about it herself.

        Oh, I would love that. I can't wait to see her.

        I really hope she has left the place by the time I see her...

<<endif>>

...Lily signs up for labeling work that's...let's say more intense...

-> Intense how?

They are for filtering and moerating purposes, a lot of very disturbing things you wouldn't see...

...abuse, torture, sexual exploitation, real death...those kinds of stuff.

It's a part of the Sea, as it is always recording. Makes me sick to just imagine those things passing through me.

-> ...oh...

It would pay well. But it takes a toll on the mind.

She burst out sobbing one day. I still remember the way her voice trembled...I felt sorry for her.

-> That's awful...shouldn't they receive psychological help?

    They do...but they are formalities first and foremost. Our performances are still measured.

    Breathing techniques, mindfulness...they offer exercises only when you ask. It's hard to see depression and anxiety within you until they become overwhelming

-> It's good that she has support, like you as her friend.

    I guess so...but I couldn't handle the way she copes...

    ...she would tell dark jokes about suicide...I told her I didn't want to hear those.

I think it made her suppress it further. She picked up smoking to numb it. I guess there's a bit of regret there for me too...now that I think about her.

-> That's surprising. She looks so self-determined in the picture. <<if $lily is 0>>

    Perhaps that determination is a form of escaping and coping.

-> It's heavy...so much burden but only she alone can handle it. <<if $lily is 1>>

    I wish I could be there for her more...I really do. But I know I would hit my limits with her. And I couldn't help it.

-> It looks like her detachment is a way of coping. <<if $lily is 2>>

    Yes, but she also toughened herself through it.

She was really enthusiatstic about her job search...almost like it's about the escape itself.

That's why she would talk about having her own company, to finally have the freedom to make her own decisions.

-> I admire Lily that way. She discovers who she is through this hardship.

    Perhaps. I haven't thought of it that way. Maybe that's how she thinks about herself too. But I'm not so sure.

    I think it's necessary for her to harden herself for survival. But I don't think she would make the right decisions from that place of suffering, either.

-> Do you think it's good to crave escapes so much?

    I'm not in a place to say, truly. I have thought about escaping in many forms many times. But I was never sure if I was ready for what's coming after it.

    But I don't blame her...she told me she would have intrusive thoughts, like really disturbing ones.

And the twitching, like her brains couldn't help but to be in a panic mode, trying to defend itself.

-> Thank you, I think we have talked enough about Lily. I don't want you to dwell on it for too long, Mey.

Sure, I'm sure there's another memory of her in the Sea somehwere, maybe you can find it.

Sorry...I need a few seconds. It hurts to think about her.

<<jump Act_2_Hub>>

===
